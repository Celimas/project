(* Grako Grammar for Tyler Marklyn's Math Language *)

math = comments decks:decks defs:definitions calcs:calculations $ ;

(* Any amount of comments at the top of the file *)
comments = { nonspace } ;

(* Rules for importing decks *)
decks = "Decks" ':' { @+:deck_name } ; 

deck_name = nonspace ;

(* Rules for defining named combinations *)
definitions = "Definitions" ':' { @+:definition } ;

definition = name:nonspace '=' sets:card_sets ;

card_sets = set:card_set { with+:secondary_card_set } ;

card_set = [ num:numeric 'x' ] '(' trait:alphanumeric ',' val:value ')' ;

value = "Same" | "Different" | alphanumeric ;

secondary_card_set = conj:conjunction set:card_set ;

conjunction = "Seperate" | "Shared" | "Or" ;


(* Rules for requesting calculations *)
calculations = "Calculations" ':' { @+:calculation } ;

calculation = '{' [ "Cards" ':' cards:numeric ]
                  [ "Known" ':' known:calc_set ]
                  [ "Draws" ':' draws:numeric ]
                  [ "Odds" ':' odds:calc_set '-' exists:existence ] '}' ;

calc_set = ( variable:nonspace )
         | ( sets:card_sets ) ;

existence = "Present" | "Not-present" | "Exact" ;


(* helper rules *)
nonspace = /[^A-Z\s]+/ ;
alphanumeric = /[a-z0-9]+/ ;
numeric = /[0-9]+/ ;